// SPDX-License-Identifier: Apache-2.0
// Copyright (c) 2026 QueMusic Contributors
//
import QtQuick
import Qt5Compat.GraphicalEffects
import QueMusic 1.0

Window {
    id: desktopLyricsWindow
    width: 640
    height: 160
    minimumWidth: 480
    minimumHeight: 120
    visible: true
    color: "transparent"
    title: "QueMusic 桌面歌词"
    transientParent: null
    flags: Qt.Window | Qt.FramelessWindowHint | Qt.WindowStaysOnTopHint
    property bool active: true   // 外部控制显示/隐藏
    property int lyricSize: height / 4 - 18

    // 宿主注入：播放引擎不再靠上下文继承访问宿主的局部 id
    readonly property AudioEngine player: Playback.player

    // 歌词数据引用
    property var lyricsData: MusicApi.lyricsData || []
    property int currentIndex: 0
    property int nextIndex: 1

    // 时间格式化（复用）
    function formatTime(ms: real): string {
        if (isNaN(ms) || ms < 0) return "00:00";
        const totalSeconds = Math.floor(ms / 1000);
        const minutes = Math.floor(totalSeconds / 60);
        const seconds = totalSeconds % 60;
        return (minutes < 10 ? "0" : "") + minutes + ":" + (seconds < 10 ? "0" : "") + seconds;
    }

    // 更新歌词索引
    function updateCurrentIndex(): void {
        const pos = player.position || 0;
        const data = lyricsData;
        if (!data || data.length === 0) {
            currentIndex = -1;
            nextIndex = -1;
            return;
        }
        let idx = 0;
        while (idx + 1 < data.length && pos >= data[idx + 1].time) idx++;
        currentIndex = idx;
        nextIndex = Math.min(idx + 1, data.length - 1);
    }

    // 定时更新
    Timer {
        interval: 240
        running: player.onMedia && desktopLyricsWindow.visible
        repeat: true
        onTriggered: updateCurrentIndex()
    }

    // 歌词数据变化时重置
    Connections {
        target: MusicApi
        function onLyricsDataChanged(): void {
            lyricsData = MusicApi.lyricsData || [];
            updateCurrentIndex();
        }
    }

    // 歌词文本容器（两行）
    Column {
        id: lyricsColumn
        y: 60
        width: parent.width
        height: parent.height - 60
        spacing: 8
        z: 5

        Text {
            id: currentLineText
            width: parent.width
            text: (desktopLyricsWindow.currentIndex >= 0 && desktopLyricsWindow.currentIndex < lyricsData.length)
                  ? lyricsData[desktopLyricsWindow.currentIndex].text || ""
                  : "🎵 纯音乐，请欣赏"
            font.pixelSize: desktopLyricsWindow.lyricSize * 1.2
            font.bold: true
            font.weight: Font.Medium
            color: Style.themes.themeColor
            elide: Text.ElideRight
            horizontalAlignment: Text.AlignHCenter
            layer.enabled: true
            layer.effect: DropShadow {
                radius: 12.0
                samples: 16
                fast: true
                color: "#41000000"
            }
        }

        Text {
            id: nextLineText
            width: parent.width
            text: (desktopLyricsWindow.nextIndex >= 0 && desktopLyricsWindow.nextIndex < lyricsData.length)
                  ? lyricsData[desktopLyricsWindow.nextIndex].text || ""
                  : ""
            font.pixelSize: desktopLyricsWindow.lyricSize
            font.bold: true
            color: "#fcfcfc"
            elide: Text.ElideRight
            horizontalAlignment: Text.AlignHCenter
            visible: text !== ""
            layer.enabled: true
            layer.effect: DropShadow {
                radius: 12.0
                samples: 16
                fast: true
                color: "#56000000"
            }
        }
    }

    property int bw: 3

    MouseArea {
        anchors.fill: parent
        hoverEnabled: true
        z: 1
        cursorShape: {
            const p = Qt.point(mouseX, mouseY);
            const b = bw + 10; // Increase the corner size slightly
            if (p.x < b && p.y < b) return Qt.SizeFDiagCursor;
            if (p.x >= width - b && p.y >= height - b) return Qt.SizeFDiagCursor;
            if (p.x >= width - b && p.y < b) return Qt.SizeBDiagCursor;
            if (p.x < b && p.y >= height - b) return Qt.SizeBDiagCursor;
            if (p.x < b || p.x >= width - b) return Qt.SizeHorCursor;
            if (p.y < b || p.y >= height - b) return Qt.SizeVerCursor;
        }
        acceptedButtons: Qt.NoButton // don't handle actual events
    }

    DragHandler {
        id: resizeHandler
        grabPermissions: TapHandler.TakeOverForbidden
        target: null
        onActiveChanged: if (active) {
                             const p = resizeHandler.centroid.position;
                             const b = bw + 10; // Increase the corner size slightly
                             let e = 0;
                             if (p.x < b) { e |= Qt.LeftEdge }
                             if (p.x >= width - b) { e |= Qt.RightEdge }
                             if (p.y < b) { e |= Qt.TopEdge }
                             if (p.y >= height - b) { e |= Qt.BottomEdge }
                             desktopLyricsWindow.startSystemResize(e);
                         }
    }


    Rectangle {
        opacity: desktopLyricArea.containsMouse ? 1 : 0
        anchors.fill: parent
        color: "#88000000"
        radius: Style.settings.cubeRadius
        z: 2
        Behavior on opacity { NumberAnimation { duration: 120 } }
        Text {
            x: 20
            y: 12
            height: 36
            width: desktopLyricsWindow.width / 2 - 80
            text: Playback.musicTitle + " - " + Playback.musicArtist
            verticalAlignment: Text.AlignVCenter
            color: "#fffafafa"
            font.pixelSize: Style.settings.text
        }
        // 拖动区域
        MouseArea {
            id: desktopLyricArea
            anchors.fill: parent
            anchors.margins: 6
            property real dragOffsetX: 0
            property real dragOffsetY: 0
            onPressed: (mouse) => {
                dragOffsetX = mouse.x;
                dragOffsetY = mouse.y;
            }
            onPositionChanged: (mouse) => {
                if (pressed) {
                    desktopLyricsWindow.x += mouse.x - dragOffsetX;
                    desktopLyricsWindow.y += mouse.y - dragOffsetY;
                }
            }
            hoverEnabled: true

            // 控制按钮组
            Row {
                y: 6
                anchors.horizontalCenter: parent.horizontalCenter
                spacing: 6

                // 上一首
                SButton {
                    width: 36; height: 36; radius: Style.settings.labelRadius
                    iconCharacter: "\uf0dc"
                    iconSize: 15
                    buttonColor: "transparent"
                    hoverColor: "#66fafafa"
                    iconColor: "#fffdfdfd"
                    shadowEnabled: false
                    onClicked: Playback.previous()
                    QTip { visible: parent.hovered; text: "上一首" }
                }

                // 播放/暂停
                SButton {
                    width: 36; height: 36; radius: Style.settings.labelRadius
                    iconCharacter: player.playing ? "\uf02f" : "\uf00e"
                    iconSize: 16
                    buttonColor: "transparent"
                    hoverColor: "#66fafafa"
                    iconColor: "#fffdfdfd"
                    shadowEnabled: false
                    onClicked: Playback.togglePlay()
                    QTip { visible: parent.hovered; text: player.playing ? "暂停" : "播放" }
                }

                // 下一首
                SButton {
                    width: 36; height: 36; radius: Style.settings.labelRadius
                    iconCharacter: "\uf0d9"
                    iconSize: 15
                    buttonColor: "transparent"
                    hoverColor: "#66fafafa"
                    iconColor: "#fffdfdfd"
                    shadowEnabled: false
                    onClicked: Playback.next(false)
                    QTip { visible: parent.hovered; text: "下一首" }
                }
            }
            // 进度条小提示（时间）
            Text {
                x: desktopLyricsWindow.width - 68 - width
                y: 6
                height: 36
                text: desktopLyricsWindow.formatTime(player.position) + " / " + desktopLyricsWindow.formatTime(player.duration)
                font.pixelSize: 13
                verticalAlignment: Text.AlignVCenter
                color: "#fffdfdfd"
            }
            // 关闭按钮
            SButton {
                id: closeButton
                x: desktopLyricsWindow.width - 54
                y: 6
                width: 36; height: 36; radius: Style.settings.labelRadius
                iconCharacter: "\uf025"
                iconSize: 15
                buttonColor: "transparent"
                hoverColor: "#66fafafa"
                iconColor: "#fffdfdfd"
                shadowEnabled: false
                onClicked: {
                    desktopPlayer.desktopPlayerMode = 0;
                    desktopLyricsLoader.active = false;
                }

                QTip {
                    visible: parent.hovered
                    text: "关闭"
                }
            }
        }
    }

    // 窗口可见性跟随 active
    onActiveChanged: visible = active;

    Component.onCompleted: {
        updateCurrentIndex();
        // 默认显示
        active = true;
    }
}
